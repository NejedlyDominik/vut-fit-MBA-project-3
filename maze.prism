// MBA 2022/2023

dtmc

//  5 |         .e...
//  4 | ....  . .   .
//  3 | .  .  . .   .
//  2 | . .s..s.s.. .
//  1 | e  .  . . ...
//  0 | ....  ...    
//  y ---------------
//    x 0123456789012


// macro evaluated to true when it is possible to go up
formula u = (x=0&y=0)|(x=3&y=0)|(x=6&y=0)|(x=8&y=0)|(x=10&y=0)|(x=11&y=0)|(x=12&y=0)|(x=0&y=1)|(x=2&y=1)|(x=3&y=1)|(x=4&y=1)|(x=5&y=1)|(x=6&y=1)|(x=7&y=1)|(x=8&y=1)|(x=9&y=1)|(x=10&y=1)|(x=12&y=1)|(x=0&y=2)|(x=3&y=2)|(x=6&y=2)|(x=8&y=2)|(x=12&y=2)|(x=0&y=3)|(x=1&y=3)|(x=2&y=3)|(x=3&y=3)|(x=6&y=3)|(x=8&y=3)|(x=12&y=3)|(x=8&y=4)|(x=9&y=4)|(x=10&y=4)|(x=11&y=4)|(x=12&y=4);

// macro evaluated to true when it is possible to go right
formula r = (x=0&y=0)|(x=1&y=0)|(x=2&y=0)|(x=5&y=0)|(x=6&y=0)|(x=7&y=0)|(x=2&y=1)|(x=5&y=1)|(x=7&y=1)|(x=9&y=1)|(x=10&y=1)|(x=11&y=1)|(x=1&y=2)|(x=2&y=2)|(x=3&y=2)|(x=4&y=2)|(x=5&y=2)|(x=6&y=2)|(x=7&y=2)|(x=8&y=2)|(x=9&y=2)|(x=11&y=2)|(x=2&y=3)|(x=5&y=3)|(x=7&y=3)|(x=11&y=3)|(x=0&y=4)|(x=1&y=4)|(x=2&y=4)|(x=5&y=4)|(x=7&y=4)|(x=11&y=4)|(x=7&y=5)|(x=8&y=5)|(x=9&y=5)|(x=10&y=5)|(x=11&y=5);

// macro evaluated to true when it is possible to go down
formula d = (x=0&y=1)|(x=1&y=1)|(x=2&y=1)|(x=3&y=1)|(x=6&y=1)|(x=7&y=1)|(x=8&y=1)|(x=0&y=2)|(x=3&y=2)|(x=6&y=2)|(x=8&y=2)|(x=10&y=2)|(x=11&y=2)|(x=12&y=2)|(x=0&y=3)|(x=2&y=3)|(x=3&y=3)|(x=4&y=3)|(x=5&y=3)|(x=6&y=3)|(x=7&y=3)|(x=8&y=3)|(x=9&y=3)|(x=10&y=3)|(x=12&y=3)|(x=0&y=4)|(x=3&y=4)|(x=6&y=4)|(x=8&y=4)|(x=12&y=4)|(x=0&y=5)|(x=1&y=5)|(x=2&y=5)|(x=3&y=5)|(x=6&y=5)|(x=8&y=5)|(x=12&y=5);

// macro evaluated to true when it is possible to go left
formula l = (x=1&y=0)|(x=2&y=0)|(x=3&y=0)|(x=4&y=0)|(x=7&y=0)|(x=8&y=0)|(x=9&y=0)|(x=1&y=1)|(x=4&y=1)|(x=7&y=1)|(x=9&y=1)|(x=11&y=1)|(x=12&y=1)|(x=1&y=2)|(x=3&y=2)|(x=4&y=2)|(x=5&y=2)|(x=6&y=2)|(x=7&y=2)|(x=8&y=2)|(x=9&y=2)|(x=10&y=2)|(x=11&y=2)|(x=1&y=3)|(x=4&y=3)|(x=7&y=3)|(x=9&y=3)|(x=1&y=4)|(x=2&y=4)|(x=3&y=4)|(x=4&y=4)|(x=7&y=4)|(x=9&y=4)|(x=9&y=5)|(x=10&y=5)|(x=11&y=5)|(x=12&y=5);

// target cells
formula exit = (x=0&y=1) | (x=9&y=5);


// synchronization module: place - steer-drive - steer-drive - ...
module clk
    clk : [-1..2] init -1;
    [place] clk=-1 -> (clk'=0);
    [steer] clk=0 -> (clk'=1);
    [drive] clk=1 -> (clk'=0);
endmodule


// names for four principal directions
const int up = 0;
const int right = 1;
const int down = 2;
const int left = 3;

const double succ_move_prob = 0.9;

// macros encoding movement in the selected direction or bumping into a wall
formula yu = u ? (y+1) : y;
formula xr = r ? (x+1) : x;
formula yd = d ? (y-1) : y;
formula xl = l ? (x-1) : x;

module maze
    x : [-1..12];
    y : [-1..5];

    // random initial placement
    [place] true -> 1/3:(x'=3)&(y'=2) + 1/3:(x'=6)&(y'=2) + 1/3:(x'=8)&(y'=2);

    // (DEBUG) deterministic placement
    //[place] true -> (x'=3)&(y'=2);

    // moving in the direction selected by the controller
    [drive] dir=up    -> succ_move_prob: (y'=yu) + (1 - succ_move_prob): (x'=xl);
    [drive] dir=right -> succ_move_prob: (x'=xr) + (1 - succ_move_prob): (y'=yu);
    [drive] dir=down  -> succ_move_prob: (y'=yd) + (1 - succ_move_prob): (x'=xr);
    [drive] dir=left  -> succ_move_prob: (x'=xl) + (1 - succ_move_prob): (y'=yd);
endmodule


module controller
    // steering direction
    dir : [0..3] init 0;

    // sample strategy 1: alternate between steering down and left
    //steering_down : bool init false;
    //[steer] steering_down -> (dir'=down) & (steering_down'=false);
    //[steer] !steering_down -> (dir'=left) & (steering_down'=true);

    // sample strategy 2: steer up if possible, otherwise steer randomly left/right
    //[steer] u -> (dir'=up);
    //[steer] !u -> 1/2:(dir'=left) + 1/2:(dir'=right);

    // basic random strategy
    //[steer] true -> 1/4:(dir'=left) + 1/4:(dir'=right) + 1/4:(dir'=up) + 1/4:(dir'=down);

    strategy_option : [0..4] init 0;

    // strategy 0: for S(8,2) to E(9,5) -- default strategy
    [steer] u & strategy_option=0 -> (dir'=up);
    [steer] !u & r & (l | d) & strategy_option=0 -> (dir'=right);

    // strategy switch according to place (6,4) from strategy 0 to strategy 1
    [steer] !u & !r & !l & strategy_option=0 -> (dir'=down) & (strategy_option'=1);

    // strategy 1: for S(6,2) to place (7,2) with strategy switch to 0
    [steer] !r & strategy_option=1 -> (dir'=down);
    [steer] u & r & strategy_option=1 -> (dir'=right);
    [steer] !u & r & strategy_option=1 -> (dir'=right) & (strategy_option'=0);

    // strategy switch according to place (3,4) from strategy 0 to strategy 2
    [steer] !u & !r & l & strategy_option=0 -> (dir'=left) & (strategy_option'=2);

    // strategy 2: for S(3,2) to place (0,4)
    [steer] u & strategy_option=2 -> (dir'=up);
    [steer] !u & l & strategy_option=2 -> (dir'=left);

    // strategy switch according to place (0,4) from strategy 2 to strategy 3
    [steer] !u & !l & strategy_option=2 -> (dir'=down) & (strategy_option'=3);

    // strategy 3: for S(3,2) from place (0,4) to E(0,1)
    [steer] d & strategy_option=3 -> (dir'=down);
    [steer] !d & strategy_option=3 -> (dir'=left);

    // strategy switch according to place (2,2) from strategy 0 to strategy 4
    [steer] !u & r & !l & !d & strategy_option=0 -> (dir'=right) & (strategy_option'=4);

    // strategy 4: for S(3,2) to E(0,1)
    [steer] d & strategy_option=4 -> (dir'=down);
    [steer] !d & l & strategy_option=4 -> (dir'=left);
    [steer] !d & !l & u & strategy_option=4 -> (dir'=up);
    [steer] !d & !l & !u & strategy_option=4 -> (dir'=right);
endmodule

rewards "steps"
    [drive] true : 1;
endrewards
